#Область ЭкспортныеПроцедурыИФункции

&НаКлиенте
Функция ДанныеШаговДляРедактора(ТаблицаИзвестныхStepDefinition, МодульПарсерФич) Экспорт

	ДанныеФичи = Новый Структура;
	ДанныеФичи.Вставить("ЯзыкФичи", "ru");
		
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоМассива();
	Для Каждого СтрокаТаблицаИзвестныхStepDefinition Из ТаблицаИзвестныхStepDefinition Цикл
		Если ПустаяСтрока(СтрокаТаблицаИзвестныхStepDefinition.ТипШага) Тогда
			Продолжить;
		КонецЕсли;	 
		Если ПустаяСтрока(СтрокаТаблицаИзвестныхStepDefinition.ОписаниеШага) Тогда
			Продолжить;
		КонецЕсли;	 
		
		ВтораяЧастьСтрокиПозиция = -1;
		МодульПарсерФич.ПолучитьКлючевоеСлово(ДанныеФичи,
			СтрокаТаблицаИзвестныхStepDefinition.ПредставлениеТеста, ВтораяЧастьСтрокиПозиция);
		ИмяШагаБезКлючевогоСлова = СокрЛП(Сред(СтрокаТаблицаИзвестныхStepDefinition.ПредставлениеТеста, ВтораяЧастьСтрокиПозиция));
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("filterText");
		ЗаписьJSON.ЗаписатьЗначение(ИмяШагаБезКлючевогоСлова);
		ЗаписьJSON.ЗаписатьИмяСвойства("insertText");
		ЗаписьJSON.ЗаписатьЗначение(СтрокаТаблицаИзвестныхStepDefinition.ПредставлениеТеста);
		ЗаписьJSON.ЗаписатьИмяСвойства("sortText");
		ЗаписьJSON.ЗаписатьЗначение("");
		ЗаписьJSON.ЗаписатьИмяСвойства("documentation");
		ЗаписьJSON.ЗаписатьЗначение(СтрокаТаблицаИзвестныхStepDefinition.ОписаниеШага);
		ЗаписьJSON.ЗаписатьИмяСвойства("kind");
		ЗаписьJSON.ЗаписатьЗначение(ТипШага(СтрокаТаблицаИзвестныхStepDefinition));
		ЗаписьJSON.ЗаписатьИмяСвойства("section");
		ЗаписьJSON.ЗаписатьЗначение(СтрокаТаблицаИзвестныхStepDefinition.ТипШага);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;	 
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	Данные = ЗаписьJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции	 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ТипШага(ДанныеШага)
	Результат = 0;
	
	Если ДанныеШага.ТипШагаВДереве = "Условие" Тогда
		Результат = 5;
	ИначеЕсли ДанныеШага.ТипШагаВДереве = "Цикл" Тогда
		Результат = 8;
	ИначеЕсли Найти(ДанныеШага.ПредставлениеТеста, "в течение") Тогда
		Результат = 7;
	ИначеЕсли Найти(ДанныеШага.ПредставлениеТеста, "файл") > 0 Тогда
		Результат = 23;
	ИначеЕсли Найти(ДанныеШага.ТипШага, "Прочее.") > 0 Тогда
		Результат = 4;
	КонецЕсли;	 
	
	Возврат Результат;
КонецФункции	 

#КонецОбласти